; RND Speedup

SYNTAXZ:EQU	0x2530
S_PIEND:EQU	0x2630
STACKBC:EQU	0x2D2F
RESTACK:EQU	0x329A
SEED:	EQU	0x5C76

	ORG	0x25F8
	CALL	SYNTAXZ
	JR	Z,S_PIEND
	LD	HL,(SEED)
	LD	E,L
	LD	D,H
	XOR	A
	LD	BC,0x62C
RNDL:	ADC	HL,HL
	ADC	A,A
	RL	C
	JR	NC,NOADD
	CCF
	ADC	HL,DE
	ADC	A,0
NOADD:	DJNZ	RNDL
	LD	C,75
	ADC	HL,BC
	ADC	A,B
	CALL	S_SEED
	LD	A,(HL)
	OR	A
	JR	Z,S_PIEND
	SUB	A, 0x10
	LD	(HL),A
	JP	S_PIEND
	NOP

; External routine, need to find extra 18 bytes for it elsewhere
S_SEED:	JR	Z,NOMOD
	LD	C,A
	SBC	HL,BC
	JR	C,DOMOD
NOMOD:	DEC	HL
DOMOD:	LD	(SEED),HL
	LD	C,L
	LD	B,H
	CALL	STACKBC
	JP	RESTACK
