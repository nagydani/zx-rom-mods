	DEFB	P_LABEL - $	; 2' @ / LABEL / DEF PROC
	DEFB	P_ONERROR - $	; 1' ON ERROR
	DEFB	P_WRITE - $	; 8' WRITE #
	DEFB	P_DELETE - $	; A  DELETE
	DEFB	P_LOCAL - $	; B  LOCAL
	DEFB	P_CLIP - $	; C  CLIP
	DEFB	P_DISPLAY - $	; D  DISPLAY
	DEFB	P_FPOKE - $	; E  FPOKE
	DEFB	P_RENUM - $	; F  RENUM
	DEFB	P_SOUND - $	; G  SOUND
	DEFB	P_ENDWHILE - $	; H  END WHILE
	DEFB	P_UNTIL - $	; I  UNTIL
	DEFB	P_TRACE - $	; J  TRACE
	DEFB	P_ENDIF - $	; K  END IF
	DEFB	P_YIELD - $	; L  YIELD
	DEFB	P_REPEAT - $	; M  REPEAT
	DEFB	P_EXIT - $	; N  EXIT
	DEFB	P_WHILE - $	; O  WHILE
	DEFB	P_ENDPROC - $	; P  END PROC
	DEFB	P_STACK - $	; Q  STACK
	DEFB	P_PROC - $	; R  PROC
	DEFB	P_POP - $	; S  POP
	DEFB	P_SPECTRUM - $	; T  SPECTRUM
	DEFB	P_PLAY - $	; U  PLAY
P_END:	EQU	$

P_LABEL:
P_ONERROR:
P_WRITE:
P_DELETE:
P_LOCAL:
P_CLIP:
P_FPOKE:
P_RENUM:
P_ENDWHILE:
P_UNTIL:
P_TRACE:
P_ENDIF:
P_YIELD:
P_REPEAT:
P_EXIT:
P_WHILE:
P_ENDPROC:
P_STACK:
P_PROC:
P_POP:
	DEFB	$05
	DEFW	ERROR_C

P_SOUND:DEFB	$05
	DEFW	SOUND

P_DISPLAY:
	DEFB	$03
	DEFW	DISPLAY

P_PLAY:	DEFB	$05
	DEFW	PLAY

P_SPECTRUM:
	DEFB	$00
	DEFW	SPECTRUM

CHECK_END:
	CALL	SYNTAX_Z
	RET	NZ
END05_E:POP	BC		; SCAN-LOOP
END05:	POP	BC		; STMT-RET
STMT_NEXT:
	RST	$18
	CP	$0D		; CR
	JR	Z,LINE_END
	CP	":"
	JR	Z,STMT_LOOP
	JR	ERROR_C_I

STMT_LOOP:
	LD	HL,L1B28	; STMT-LOOP
	PUSH	HL
	RST	$10

LINE_END:
	BIT	7,(IY+$01)
	JR	Z,LE_SWAP
	LD	HL,(NXTLIN)
	LD	A,$C0
	AND	(HL)
	JR	NZ,LE_SWAP	; program finished
	PUSH	HL
	LD	HL,L1BBF - 1	; XOR A, LINE-USE
	EX	(SP),HL
LE_SWAP:RST	$10

CMDCLASS2:
	DEFB	CLASS2_00 - $	; parameterless instruction
	DEFB	CLASS2_01 - $	; do something to a variable
	DEFB	CLASS2_02 - $	; prepare value for assignment
	DEFB	CLASS2_03 - $	; one numeric parameter, defaults to zero
	DEFB	CLASS2_04 - $	; used by FOR & NEXT -- TODO: may be worth replacing
	DEFB	CLASS2_05 - $	; list of items
	DEFB	CLASS2_06 - $	; evaluate single numeric expression
	DEFB	CLASS2_07 - $	; open #2 or other stream before execution
	DEFB	CLASS2_08 - $	; two numeric expressions, separated by comma

CLASS2_03:
	CALL	FETCH_NUM
CLASS2_00:
	CP	A
CLASS2_05:
	POP	BC
	CALL	Z,CHECK_END
	EX	DE,HL
	LD	HL,(T_ADDR)
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	EX	DE,HL
	PUSH	BC
	RET

CLASS2_01:
	RST	$30
	DEFW	L1C1F		; CLASS_01
	RET

CLASS2_02:
	RST	$30
	DEFW	L1C4E		; CLASS_02
	RET

CLASS2_04:
	RST	$30
	DEFW	L1C6C		; CLASS_04
	RET

NEXT_2NUM:
	RST	$20

CLASS2_08:
	CALL	CLASS2_06
	CP	","
	JR	NZ,ERROR_C_I
NEXT_1NUM:
	RST	$20

CLASS2_06:
	RST	$30
	DEFW	L24FB		; SCANNING
	BIT	6,(IY+$01)
	RET	NZ

ERROR_C_I:
	JP	ERROR_C

FETCH_NUM:
	CP	$0D
	JR	Z,USE_ZERO
	CP	":"
	JR	NZ,CLASS2_06

USE_ZERO:
	RST	$30
	DEFW	L1CE6		; USE-ZERO
	RET

CLASS2_07:
	RST	$30
	DEFW	L2070		; STR-ALTER
	RET	NC
	LD	HL,(T_ADDR)
	LD	A,(HL)
	OR	A
	JR	Z,CL7_E
	INC	HL
	LD	(T_ADDR),HL
CL7_E:	CALL	UNSTACK_Z
	LD	A,2
	RST	$30
	DEFW	L1601	; CHAN-OPEN
	RET

; instruction routines
	INCLUDE	"display.asm"
	INCLUDE	"spectrum.asm"
	INCLUDE	"play.asm"
	INCLUDE	"sound.asm"
