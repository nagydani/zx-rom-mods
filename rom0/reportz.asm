REPORT:	CP	MAX_ERR
	JR	C,REPORTZ
	SUB	$81
	LD	(ERR_NR),A
	EX	DE,HL
REPORTL:LD	A,(DE)		; Find end of command line
	INC	DE
	CP	$80
	JR	Z,MESSAGE
	CP	$0E
	JR	NZ,REPORTL
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	JR	REPORTL

STDERR_MSG:
	XOR	A
	PUSH	DE
	RST	$30
	DEFW	L1601
	POP	DE
	JR	MESSAGE

; Output token
; In: A=token code
TOKEN_O:SUB	$7F
	EX	DE,HL
	BIT	3,(HL)
TOKEN_L:CALL	Z,PR_SPACE
	LD	DE,T_ELSE
	JR	NZ,TOKEN_N	; jump forward in argument mode
	CP	ELSE_T - $7F
	JR	Z,TOKEN_E
	SET	2,(IY+FLAGS-ERR_NR)
TOKEN_N:LD	B,A
	SUB	INSTRUCTION_T - $7F
	JR	C,TOKEN		; jump forward for operators
	SUB	RND_T - INSTRUCTION_T
	JR	NC, TOKEN1	; token in ROM1
	CALL	TOKEN
	RRCA
	RST	$30
	DEFW	L2C8D		; ALPHA -- ALPHANUM would be more correct, but slow and irrelevant
	RET	NC
PR_SPACE:
	BIT	0,(IY+$01)	; space suppressed?
	JR	NZ,ZRET		; return with ZF, if so
	PUSH	HL
	PUSH	AF
	LD	A," "
	RST	$30
	DEFW	L0C3B		; PO_SAVE
	EX	DE,HL
	POP	AF
	POP	HL
ZRET:	CP	A		; set ZF
	RET

TOKEN1:	RST	$30
	DEFW	L0C10
	RET

TOKEN_E:RES	2,(IY+FLAGS-ERR_NR)	; K mode next
	JR	TOKEN_S

REPORTZ:SUB	$1C
	LD	B,A
	INC	B
	ADD	"S"
	RST	$30
	DEFW	L0010
	LD	A," "
	RST	$30
	DEFW	L0010
	LD	DE,REPORTS
TOKEN:	LD	A,(DE)
	ADD	A,A
	INC	DE
	JR	NC,TOKEN
	RET	Z		; end of token table
	DJNZ	TOKEN
TOKEN_S:LD	A,(DE)
	CP	" "
	JR	NZ,MSGNSP
MSG_SP:	BIT	0,(IY+$01)
	JR	NZ,MSGSKIP
MESSAGE:LD	A,(DE)
MSGNSP:	AND	$7F
	RST	$30
	DEFW	L0C3B		; PO-SAVE
	LD	A,(DE)
MSGSKIP:INC	DE
	ADD	A,A
	JR	NC,MESSAGE
	RES	0,(IY+$01)	; allow leading space
	CP	$40		; 2 * " "
	RET	NZ
	INC	A		; clear Z
NOLEAD:	SET	0,(IY+$01)	; suppress leading space
	RET

SYNTAX_Z:
	BIT	7,(IY+1)
	RET

UNSTACK_Z:
	CALL	SYNTAX_Z
	POP	HL
	RET	Z
	JP	(HL)

