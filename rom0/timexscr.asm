SCR_ALL:LD	B,$17
CL_SCR:	LD	A,(S_MODE)
	OR	A
	JR	NZ,SCROLL
	RST	$28
	DEFW	L0E00		; CL-SCROLL
	RET
SCROLL:	INC	B
	RST	$28
	DEFW	L0E9B
SCR_L:	PUSH	BC
	PUSH	HL
	JR	SCR_S
SCR_R:	PUSH	BC
	LD	BC,$0020
	LDIR
	SET	5,H
	SET	5,D
	LD	C,$20
	DEC	HL
	DEC	DE
	LDDR
	RES	5,H
	INC	HL
	POP	BC
SCR_S:	LD	D,H
	LD	E,L
	LD	A,L
	ADD	A,$20
	LD	L,A
	JR	NC,SCR_LT
	LD	A,H
	ADD	A,$08
	LD	H,A
SCR_LT:	DJNZ	SCR_R
	POP	HL
	POP	BC
	INC	H
	LD	A,$07
	AND	H
	JR	NZ,SCR_L
	LD	A,H
	SUB	$08
	LD	H,A
	LD	B,1
CLLINE:	LD	A,(S_MODE)
	OR	A
	JR	NZ,SCRCLL
	RST	$28
	DEFW	L0E44
	RET

SCRCLL:	CP	2
	LD	A,(BORDCR)
	JR	NZ,CLL1
	XOR	A
CLL1:	PUSH	BC
CLLL:	PUSH	BC
	PUSH	AF
	RST	$28
	DEFW	L0E9B		; CL-ADDR
	POP	AF
SCR_MC:	LD	E,L
	LD	D,H
	INC	DE
	LD	(HL),0
	LD	C,$1F
	LDIR
	SET	5,H
	LD	E,L
	LD	D,H
	LD	(HL),A
	LD	C,$1F
	LDDR
	RES	5,H
	LD	C,A
	INC	H
	LD	A,$07
	AND	H
	LD	A,C
	JR	NZ,SCR_MC
	POP	BC
	DJNZ	CLLL
	POP	BC
	OR	A
	LD	A,B
	RET	Z
	PUSH	BC
	CALL	BC32A
	EX	AF,AF'
	DEC	BC
	LD	HL,$5AFF
	LD	DE,$5AFE
	LD	(HL),A
	LDDR
	POP	BC
	RET

BC32A:	RRCA
	RRCA
	RRCA
	LD	B,A
	AND	$E0
	LD	C,A
	XOR	B
	LD	B,A
	RET

POSCR:	LD	DE,CLSET
	PUSH	DE
	LD	A,B
	BIT	0,(IY+$02)
	JP	NZ,POSCR4
	CP	(IY+$31)
	JP	C,ERROR_5
	RET	NZ
	BIT	4,(IY+$02)
	JR	Z,POSCR2
	LD	E,(IY+$2D)
	DEC	E
	JR	Z,POSCR3
	LD	A,0
	RST	$28
	DEFW	L1601		; CHAN-OPEN
	LD	SP,(LIST_SP)
	RES	4,(IY+$02)
	JP	SWAP
POSCR2:	DEC	(IY+$52)
	JR	NZ,POSCR3
	LD	A,$18
	SUB	B
	LD	(SCR_CT),A
	LD	HL,(ATTR_T)
	PUSH	HL
	LD	A,(P_FLAG)
	PUSH	AF
	LD	DE,L0CF8	; scroll?
	RST	$28
	DEFW	MSG_WAIT
	CP	" "
	JR	Z,ERROR_D
	CP	$E2
	JR	Z,ERROR_D
	OR	$20
	CP	"n"
	JR	Z,ERROR_D
	LD	A,$FE
	RST	$28
	DEFW	L1601		; CHAN-OPEN
	POP	AF
	LD	(P_FLAG),A
	POP	HL
	LD	(ATTR_T),HL
POSCR3:	CALL	SCR_ALL
	LD	BC,(K_WIDTH)
	LD	B,(IY+$31)
	INC	B
; TODO: Attribute magic PO-SCR-3A
	RET

ERROR_D:RST	$28
	DEFW	L0D00		; BREAK - CONT repeats

POSCR4:	CP	$02
	JP	C,ERROR_5
	ADD	A,(IY+$31)
	SUB	$19
	RET	NC
	NEG
	PUSH	BC
	LD	B,A
	LD	HL,(ATTR_T)
	PUSH	HL
	LD	HL,(P_FLAG)
	PUSH	HL
	RST	$28
	DEFW	L0D4D		; TEMPS
	LD	A,B

POSCR4A:PUSH	AF
	LD	HL,DF_SZ
	LD	B,(HL)
	LD	A,B
	INC	A
	LD	(HL),A
	LD	HL,S_POSN + 1
	CP	(HL)
	JR	C,POSCR4B
	INC	(HL)
	LD	B,$17
POSCR4B:CALL	CL_SCR
	POP	AF
	DEC	A
	JR	NZ,POSCR4A
	POP	HL
	LD	(IY+$57),L
	POP	HL
	LD	(ATTR_T),HL
	LD	BC,(S_POSN)
	RES	0,(IY+$02)
	CALL	CLSET
	SET	0,(IY+$02)
	POP	BC
	RET
